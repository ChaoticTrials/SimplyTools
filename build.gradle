plugins {
    id 'org.moddingx.modgradle.meta' version '5.0.2'
}

repositories {
    maven {
        url = "https://modmaven.dev/"
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
}

var dev = System.getenv('IS_DEV')?.toBoolean() ?: false
var baseVersion = '21.1'
var libxVersion = '1.21.1-6.0.1'
var jeiVersion = '19.21.0.247'
var morevanillalibVersion = '21.1.3'
var morevanillatoolsVersion = '21.1.1'

mod.configure {
    modid 'simplytools'
    group 'de.melanx'
    versioning {
        if (dev) {
            def gitCommitHash = ['git', 'rev-parse', 'HEAD'].execute().text.trim()
            constant baseVersion + (gitCommitHash ? "-${gitCommitHash}" : '')
        } else {
            base baseVersion
            maven 'https://maven.melanx.de/release'
        }
    }

    neoforge project.hasProperty('neo_version') ? project.neo_version : "${baseVersion}.118"

    license 'The Apache License, Version 2.0'
    github 'ChaoticTrials/SimplyTools'

    artifacts {
        sources {
            publishToRepositories()
        }
    }

    changelog {
        def lines = defaultChangelog.toString().split('\n')
        def filteredChangelog = lines.findAll { !it.startsWith('- [meta]') }.join('\n')

        return filteredChangelog
    }


    publishing {
        maven {
            name 'melanx'
            url 'https://maven.melanx.de/release'
            credentials(PasswordCredentials)
        }
    }

    upload {
        all {
            type 'release'
            require 'libx'
            optional 'morevanillalib'
            optional 'morevanillatools'
            optional 'jei'
            optional 'simplest-copper-gear'
        }
        curseforge {
            projectId 610058
        }
        modrinth {
            projectId 'v7uAVl86'
        }
    }
}

dependencies {
    annotationProcessor "org.moddingx:LibX:${libxVersion}"

    implementation "org.moddingx:LibX:${libxVersion}"
    implementation "mezz.jei:jei-1.${baseVersion}-neoforge:${jeiVersion}"

    implementation "de.melanx:MoreVanillaLib:${morevanillalibVersion}"
    implementation "de.melanx:MoreVanillaTools:${morevanillatoolsVersion}"

    implementation "curse.maven:simplest-copper-gear-984803:6110163" // Simplest Copper Gear v1.0.6
//    implementation curse.mod(550532, 4768456) // LieOnLion's Enderite 2.0.0 + 1.20.1
}
